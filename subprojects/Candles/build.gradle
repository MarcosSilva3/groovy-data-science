/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'groovy'

def runall = tasks.register('runAll') {
    group 'Application'
}

FileUtil.baseNames(sourceSets.main.allSource.files, ['TablesawHelper']).each { name ->
    def subtask = tasks.register("run$name", JavaExec) {
        dependsOn compileGroovy
        group 'Application'
        description "Run ${name}.groovy as a JVM application/Groovy script"
        classpath = sourceSets.main.runtimeClasspath
        mainClass = name
    }
    runall.configure {
        dependsOn subtask
    }
}

dependencies {
    implementation "org.apache.groovy:groovy:$groovy4Version"
    implementation "org.apache.groovy:groovy-dateutil:$groovy4Version"
    implementation "org.apache.groovy:groovy-ginq:$groovy4Version"
    implementation "org.apache.groovy:groovy-macro:$groovy4Version"
    implementation "tech.tablesaw:tablesaw-core:$tablesawVersion"
    implementation "tech.tablesaw:tablesaw-excel:$tablesawVersion"
    implementation "tech.tablesaw:tablesaw-aggregate:$tablesawVersion"
    implementation "org.slf4j:slf4j-simple:$slf4jVersion"
    implementation "org.apache.poi:poi-ooxml:5.2.5"
    runtimeOnly "org.apache.logging.log4j:log4j-core:$log4j2Version"
}
