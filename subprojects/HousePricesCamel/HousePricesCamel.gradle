/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'groovy'
apply plugin: 'application'

repositories {
    mavenCentral()
}

// Camel's API didn't change but the minimum JDK version did with 3.14.x being needed for JDK8
ext.camel = JavaVersion.current().isJava8() ? camelLegacyVersion : camelVersion
ext.appName = 'ExploreOutlier_ApacheCamelCSV'

dependencies {
    implementation "org.apache.groovy:groovy:$groovy4Version"
    implementation "org.apache.camel:camel-core:$camel"
    implementation "org.apache.camel:camel-csv:$camel"
    implementation("org.apache.camel:camel-groovy:$camel") {
        exclude(group: 'org.codehaus.groovy', module:'groovy')
    }
    runtimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"}

application {
    mainClass = appName
}

tasks.named('run').configure {
    description = "Run $appName as a JVM application/Groovy script"
}

tasks.register('versionInfo') {
    doLast {
        File javaHome = new File(System.getProperty('java.home'))
        logger.lifecycle "Using Java from $javaHome (version ${JavaVersion.current()})"
        logger.lifecycle "Using Camel version $camel"
    }
}

run.dependsOn versionInfo
