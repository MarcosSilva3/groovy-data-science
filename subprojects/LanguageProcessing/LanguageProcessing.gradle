/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'groovy'

repositories {
    mavenCentral()
//    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    implementation "org.codehaus.groovy:groovy-all:$groovyVersion"
    implementation "org.apache.opennlp:opennlp-tools:$opennlpVersion"
    implementation "com.github.haifengl:smile-nlp:$smileVersion"
//    implementation "com.datumbox:datumbox-framework-lib:0.8.3-SNAPSHOT"
    implementation 'com.datumbox:datumbox-framework-lib:0.8.2'
    implementation('org.apache.tika:tika-parsers-standard-package:2.0.0') {
        exclude(group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl')
    }
    runtimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
}

tasks.register('checkJdk11Compatibility') {
    doLast {
        assert JavaVersion.current().isJava11Compatible(), "JDK11+ is required but found ${JavaVersion.current()}!"
    }
}

FileUtil.baseNames(file("$projectDir/src/main/groovy"), ['ResourceHelper']).each { name ->
    task "run$name"(type: JavaExec) {
        dependsOn compileGroovy
        group 'Script'
        if (name.contains('Datumbox')) dependsOn(checkJdk11Compatibility)
        description "Run ${name}.groovy as a JVM application/Groovy script"
        classpath = sourceSets.main.runtimeClasspath
        mainClass = name
    }
}
