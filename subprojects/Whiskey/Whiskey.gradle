/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'groovy'

repositories {
    jcenter()
    mavenCentral()
}

tasks.register('checkJdk11Compatibility') {
    doLast {
        assert JavaVersion.current().isJava11Compatible(), "JDK11+ is required but found ${JavaVersion.current()}!"
    }
}

FileUtil.baseNames(file("$projectDir/src/main/groovy")).each { name ->
    task "run$name"(type: JavaExec) {
        dependsOn compileGroovy
        if (name.contains('Datumbox')) dependsOn(checkJdk11Compatibility)
        group 'Script'
        description "Run ${name}.groovy as a JVM application/Groovy script"
        classpath = sourceSets.main.runtimeClasspath
        main = name
    }
}

dependencies {
    implementation "org.codehaus.groovy:groovy-all:$groovyVersion"
    implementation "nz.ac.waikato.cms.weka:weka-stable:$wekaVersion"
    implementation "org.apache.commons:commons-csv:$commonsCsvVersion"
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation "org.slf4j:slf4j-jdk14:$slf4jVersion"
    implementation "org.encog:encog-core:3.4"
    implementation "tech.tablesaw:tablesaw-core:$tablesawVersion"
    implementation "tech.tablesaw:tablesaw-aggregate:$tablesawVersion"
    implementation "com.github.haifengl:smile-core:$smileVersion"
    implementation "com.github.haifengl:smile-io:$smileVersion"
    implementation "com.github.haifengl:smile-plot:$smileVersion"
    implementation "org.jfree:jfreechart:$jfreechartVersion"
    implementation "de.lmu.ifi.dbs.elki:elki:0.7.5"
    implementation("de.lmu.ifi.dbs.elki:elki-batik-visualization:0.7.5") {
        exclude(group: 'xml-apis', module: 'xml-apis')
    }
    implementation "com.datumbox:datumbox-framework-lib:0.8.2"
    runtimeOnly "org.bytedeco:openblas-platform:$openblasPlatformVersion"
    runtimeOnly "org.bytedeco:arpack-ng-platform:$arpackNgPlatformVersion"
}
