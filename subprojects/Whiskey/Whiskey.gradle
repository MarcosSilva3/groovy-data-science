/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'groovy'

repositories {
    mavenCentral()
}

tasks.register('installWekaPackages', JavaExec) {
    group 'Setup'
    description "Install Weka optional packages"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'InstallWekaPackages'
}

tasks.register('checkJdk11Compatibility') {
    doLast {
        assert JavaVersion.current().isJava11Compatible(), "JDK11+ is required but found ${JavaVersion.current()}!"
    }
}

FileUtil.baseNames(sourceSets.main.allSource.files, ['InstallWekaPackages']).each { name ->
    tasks.register("run$name", JavaExec) {
        dependsOn compileGroovy
        if (name.contains('Datumbox')) dependsOn(checkJdk11Compatibility)
        group 'Application'
        description "Run ${name}.groovy as a JVM application/Groovy script"
        classpath = sourceSets.main.runtimeClasspath
        mainClass = name
    }
}

dependencies {
    implementation "org.apache.groovy:groovy:$groovy4Version"
    implementation "org.apache.groovy:groovy-swing:$groovy4Version"
    implementation "nz.ac.waikato.cms.weka:weka-dev:$wekaVersion"
    // If Weka is installed along with some optional packages, add the respective Jars to classpath
    // TODO can we get Weka library to autodownload these
    implementation files(['SelfOrganizingMap', 'LVQ', 'GenClustPlusPlus', 'XMeans', 'cascadeKMeans'].collect{
            "${System.getProperty('user.home')}/wekafiles/packages/${it}/${it}.jar"})
    implementation "org.apache.commons:commons-csv:$commonsCsvVersion"
    implementation "org.apache.commons:commons-math4-legacy:$commonsMath4Version"
    implementation 'org.hipparchus:hipparchus-clustering:2.3'
    implementation "org.slf4j:slf4j-jdk14:$slf4jVersion"
    implementation "org.encog:encog-core:3.4"
    implementation "tech.tablesaw:tablesaw-core:$tablesawVersion"
    implementation "tech.tablesaw:tablesaw-aggregate:$tablesawVersion"
    implementation "com.github.haifengl:smile-core:$smileVersion"
    implementation "com.github.haifengl:smile-plot:$smileVersion"
    implementation "org.jfree:jfreechart:$jfreechartVersion"
    implementation "de.lmu.ifi.dbs.elki:elki:0.7.5"
    implementation("org.tribuo:tribuo-all:$tribuoVersion") {
        exclude(group: 'org.bytedeco', module: 'javacpp')
    }
    implementation("de.lmu.ifi.dbs.elki:elki-batik-visualization:0.7.5") {
        exclude(group: 'xml-apis', module: 'xml-apis')
    }
    implementation "com.datumbox:datumbox-framework-lib:0.8.2"
    // used by tribuo and smile
    runtimeOnly "org.bytedeco:openblas-platform:$openblasPlatformVersion"
    runtimeOnly "org.bytedeco:arpack-ng-platform:$arpackNgPlatformVersion"
}
