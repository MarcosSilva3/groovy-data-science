/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import static JavaFXUtil.checkForJavaFX

apply plugin: 'groovy'

repositories {
    mavenCentral()
}

tasks.register('checkJdk8Compatibility') {
    doLast {
        assert JavaVersion.current().isJava8(), "JDK8 is required but found ${JavaVersion.current()}!"
    }
}

dependencies {
    implementation "org.codehaus.groovy:groovy-all:$groovyVersion"
    implementation "nz.ac.waikato.cms.weka:weka-dev:$wekaVersion"
    implementation "com.opencsv:opencsv:$opencsvVersion"
    implementation "org.apache.commons:commons-csv:$commonsCsvVersion"
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation "org.apache.camel:camel-core:$camelVersion"
    implementation "org.apache.camel:camel-csv:$camelVersion"
    implementation "org.apache.camel:camel-groovy:$camelVersion"
    implementation 'sh.joinery:joinery-dataframe:1.10'
    implementation "org.knowm.xchart:xchart:$knowmXchartVersion"
    // https://web.archive.org/web/20140415021609/http://xeiam.com:80/xchart.jsp
    runtimeOnly 'com.xeiam.xchart:xchart:2.5.1' // older version of org.knowm.xchart:xchart
    implementation "org.slf4j:slf4j-jdk14:$slf4jVersion"
    implementation "tech.tablesaw:tablesaw-core:$tablesawVersion"
    implementation "tech.tablesaw:tablesaw-aggregate:$tablesawVersion"
//    runtime 'com.github.fommil.netlib:all:1.1.2'
    implementation "com.github.haifengl:smile-core:$smileVersion"
    implementation "com.github.haifengl:smile-math:$smileVersion"
    implementation "com.github.haifengl:smile-io:$smileVersion"
    implementation "com.github.haifengl:smile-plot:$smileVersion"
    runtimeOnly "org.slf4j:slf4j-jdk14:$slf4jVersion"
    runtimeOnly "org.bytedeco:openblas-platform:$openblasPlatformVersion"
    if (JavaVersion.current().isJava8() && checkForJavaFX()) {
        implementation 'org.groovyfx:groovyfx:8.0.0'
    }
}

sourceSets {
    main {
        groovy {
            if (!JavaVersion.current().isJava8() || !checkForJavaFX()) {
                println """
********************************************************
** Excluding GroovyFX scripts due to incompatible JDK **
** ${ (JavaVersion.current().isJava8() ? "No JavaFX?" :
    "Expecting Java 8 but found ${JavaVersion.current()}").padRight(50) } **
********************************************************
"""
                exclude '**/*GroovyFX.groovy'
                exclude '**/FXCheck.groovy'
            }
        }
    }
}

FileUtil.baseNames(sourceSets.main.allSource.files).each { name ->
    task "run$name"(type: JavaExec) {
        // current version of GroovyFX requires JDK8
        if (name.endsWith('GroovyFX')) dependsOn([checkJdk8Compatibility, 'runFXCheck'])
        group 'Script'
        description "Run ${name}.groovy as a JVM application/Groovy script"
        classpath = sourceSets.main.runtimeClasspath
        mainClass = name
    }
}
