/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import static JavaFXUtil.checkForJavaFX

apply plugin: 'groovy'

repositories {
    mavenCentral()
}

tasks.register('checkJdk8Compatibility') {
    doLast {
        assert JavaVersion.current().isJava8(), "JDK8 is required but found ${JavaVersion.current()}!"
    }
}

ext.camel = JavaVersion.current().isJava8() ? camelLegacyVersion : camelVersion

dependencies {
    implementation "org.codehaus.groovy:groovy-all:$groovy3Version"
    implementation "nz.ac.waikato.cms.weka:weka-dev:$wekaVersion"
    implementation "com.opencsv:opencsv:$opencsvVersion"
    implementation "org.apache.commons:commons-csv:$commonsCsvVersion"
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation "org.apache.camel:camel-core:$camel"
    implementation "org.apache.camel:camel-csv:$camel"
    implementation "org.apache.camel:camel-groovy:$camel"
    implementation 'sh.joinery:joinery-dataframe:1.10'
    implementation "org.knowm.xchart:xchart:$knowmXchartVersion"
    // https://web.archive.org/web/20140415021609/http://xeiam.com:80/xchart.jsp
    runtimeOnly 'com.xeiam.xchart:xchart:2.5.1' // older version of org.knowm.xchart:xchart
    implementation "org.slf4j:slf4j-jdk14:$slf4jVersion"
    implementation "tech.tablesaw:tablesaw-core:$tablesawVersion"
    implementation "tech.tablesaw:tablesaw-aggregate:$tablesawVersion"
//    runtime 'com.github.fommil.netlib:all:1.1.2'
    implementation "com.github.haifengl:smile-core:$smileVersion"
    implementation "com.github.haifengl:smile-math:$smileVersion"
    implementation "com.github.haifengl:smile-io:$smileVersion"
    implementation "com.github.haifengl:smile-plot:$smileVersion"
    implementation "org.tribuo:tribuo-all:$tribuoVersion"
    runtimeOnly "org.slf4j:slf4j-jdk14:$slf4jVersion"
    runtimeOnly "org.bytedeco:openblas-platform:$openblasPlatformVersion"
    if (JavaVersion.current().isJava8() && checkForJavaFX()) {
        implementation 'org.groovyfx:groovyfx:8.0.0'
    }
}

tasks.withType(GroovyCompile).configureEach {
    if (!checkForJavaFX() || !JavaVersion.current().isJava8()) {
        exclude '**/*GroovyFX.groovy'
        exclude '**/FXCheck.groovy'
        doLast {
            println """
            ********************************************************
            ** Excluding GroovyFX scripts due to incompatible JDK **
            ** ${ (JavaVersion.current().isJava8() ? "No JavaFX?" :
                        "Expecting Java 8 but found ${JavaVersion.current()}").padRight(50) } **
            ********************************************************
            """.stripIndent()
        }
    }
}

FileUtil.baseNames(sourceSets.main.allSource.files).each { name ->
    tasks.register("run$name", JavaExec) {
        group 'Script'
        // current version of GroovyFX requires JDK8
        if (name.endsWith('GroovyFX')) dependsOn([checkJdk8Compatibility, 'runFXCheck'])
        File javaHome = new File(System.getProperty('java.home'))
        logger.lifecycle "Using Java from $javaHome (version ${System.getProperty('java.version')})"
        if (name.contains('Camel')) logger.lifecycle "Using Camel version $camel"
        description "Run ${name}.groovy as a JVM application/Groovy script"
        classpath = sourceSets.main.runtimeClasspath
        mainClass = name
    }
}
