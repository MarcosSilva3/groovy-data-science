/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'groovy'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

task installWekaPackages(type: JavaExec) {
    group 'Setup'
    description "Install Weka optional packages"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'InstallWekaPackages'
}

FileUtil.baseNames(file("$projectDir/src/main/groovy")).each { name ->
    task "run$name"(type: JavaExec) {
        dependsOn compileGroovy
        group 'Script'
        description "Run ${name}.groovy as a JVM application/Groovy script"
        classpath = sourceSets.main.runtimeClasspath
        mainClass = name
    }
}

dependencies {
    implementation "org.codehaus.groovy:groovy-all:$groovyVersion"
    implementation "nz.ac.waikato.cms.weka:weka-dev:$wekaVersion"
    implementation "tech.tablesaw:tablesaw-core:$tablesawVersion"
    implementation "tech.tablesaw:tablesaw-aggregate:$tablesawVersion"
    implementation "org.knowm.xchart:xchart:$knowmXchartVersion"
    implementation "com.github.haifengl:smile-core:$smileVersion"
    implementation "org.encog:encog-core:3.4"
    implementation "com.edwardraff:JSAT:0.0.9"
    implementation 'com.github.EdwardRaff:JSATFX:master-SNAPSHOT'
    runtimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
    implementation files(['wekaDeeplearning4j'].collect{
        "${System.getProperty('user.home')}/wekafiles/packages/${it}/${it}.jar"})
}
