plugins {
    id 'groovy'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.apache.groovy:groovy:$groovy4Version"
    implementation("com.deepnetts:deepnetts-core:1.13.2") {
        exclude(group: 'org.apache.logging.log4j', module: 'log4j-core')
        exclude(group: 'org.apache.commons', module: 'commons-lang3')
        exclude(group: 'org.apache.commons', module: 'commons-configuration2')
        exclude(group: 'org.json')
    }
    runtimeOnly "org.apache.logging.log4j:log4j-to-slf4j:$log4j2Version"
    runtimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
}

sourceSets {
    main {
        groovy {
            srcDirs = ['.']
        }
    }
}

tasks.register("copyDependenciesToLib", Copy) {
    into "$buildDir/lib"
    from project.configurations.runtimeClasspath.files
    doLast {
        println "copyDependenciesToLib:\n  ${project.configurations.runtimeClasspath.files*.absolutePath.join('\n  ')}\n  ->\n  $buildDir/lib"
    }
}

