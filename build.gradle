/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'org.kordamp.gradle.project' version '0.47.0'
    id 'com.github.ben-manes.versions' version '0.42.0'
}

config {
    info {
        name = 'groovy-data-science'
        vendor = 'paulk-asert'
        description = 'Examples using Groovy for Data Science'
        links {
            website      = 'https://github.com/paulk-asert/groovy-data-science'
            issueTracker = 'https://github.com/paulk-asert/groovy-data-science/issues'
            scm          = 'https://github.com/paulk-asert/groovy-data-science.git'
        }
    }

    publishing { enabled = false }

    buildInfo {
        skipBuildRevision = true
    }

    coverage {
        jacoco { enabled = false }
    }

    licensing {
        excludes = ['*/rt-*', '*/training.*.txt', 'weights', '*.pdf',
                    '*.xlsx', '*.csv', '*_source.txt', '*/work/*', 'dummy.txt']
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }
}

ext.UNSTABLE = /^([0-9,.-]+[.-](alpha|beta|rc)[.\d-]*)|20030203.000550$/
// ignore non-stable releases
allprojects {
    tasks.named("dependencyUpdates").configure {
        gradleReleaseChannel = 'current'
        outputFormatter = 'plain'
        rejectVersionIf {
            !(it.currentVersion.toLowerCase() ==~ UNSTABLE) && it.candidate.version.toLowerCase() ==~ UNSTABLE
        }
    }
}
